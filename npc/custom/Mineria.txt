// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
//                     Miner System                      \\
//                Version 1.1 for eAthena                \\
//                  (c) 2007 by Myzter                   \\
//   Ensure MaxGotoCount (script_athena.conf) > 100000   \\
// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\

-	script	MinerSpot	-1,{
	setarray $@CommonMinerals[0],	909,1,1,1,1,1,1,1,1,1,1,1,1;
	setarray $@NormalMinerals[0],	909,7067,7300,733,910,911,912,998,999,1002,1010,1011,7053,7054,7096,7220,7321;
	setarray $@SpecialMinerales[0],	718,719,720,721,722,723,724,725,726,727,728,729,730,731,732,733,990,991,992,993,994,995,996,997,1000,1003;
	setarray $@BonusItems[0],	7539,7318,7444,7619,7620;
	setarray $@PickLocation$[0],	"[O] Centro","[/\\] Arriba","[\\/] Abajo","[<] Izquierda","[>] Derecha","[<\\] Arriba Izquierda",
					"[/>] Arriba Derecha","[</] Abajo Izquierda","[\\>] Abajo Derecha";
	setarray $@MNAccidents$[0],	"Ouchhh!!! mi ojo... snifff",
					"Arghh!!!!... mi dedo!",
					"Ouch!... una piedra cayó sobre mi cabeza...";
	setarray $@MNAccidents2$[0],	"Oh dios mio, que tuve suerte!",
					"He he he... menos mal que estoy protegido!";
	if (countitem(7318) < 1) {
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		mes "Si, este es un hermoso mineral, pero no puedo trabajar sobre el.";
		mes " ";
		mes ">> Item requerido: ^FF0000" + getitemname(7318) + " ^000000<<";
		close;
	}
	// * Remove 1 Old Pick
	delitem 7318,1;
	
	// Game Startup (don't change)
	set @HRock,rand(10,25);	// How strong is the rock
	set @HPick,rand(10,21);	// How strong is the Old Pick
	set @HWeak,rand(9);	// Weak spot in the rock
	set @HWP,0;		// Flag for Weak Point Found
	
	// ** Resistence
	set @RESIST,0;
	// Headgear
	if (isequipped(5031) || isequipped(5009))
		set @RESIST, @RESIST + 50 + getequiprefinerycnt(1);
	else {
		if (getequipisequiped(1)) set @RESIST, @RESIST + 15 + getequiprefinerycnt(1);
		if (getequipisequiped(9)) set @RESIST, @RESIST + 10;
		if (getequipisequiped(10)) set @RESIST, @RESIST + 5;
	}
	
	// * Shoes
	if (isequipped(2401) || isequipped(2402) || isequipped(2403) || isequipped(2404) || isequipped(2407) || isequipped(2416))
		set @RESIST, @RESIST + 10 + getequiprefinerycnt(6);
	else if (getequipisequiped(6))
		set @RESIST, @RESIST + 20 + getequiprefinerycnt(6);
		
	// * Hand protectors
	if (getequipid(7) == 2604 || getequipid(7) == 2615  || getequipid(7) == 2624 || getequipid(7) == 2667 || getequipid(7) == 2692 || getequipid(7) == 2701)
		set @RESIST, @RESIST + 10;
	if (getequipid(8) == 2604 || getequipid(8) == 2615  || getequipid(8) == 2624 || getequipid(8) == 2667 || getequipid(8) == 2692 || getequipid(8) == 2701)
		set @RESIST, @RESIST + 10;
	if (@RESIST > 100) set @RESIST, 100;
	
	// * Armor
	if (getequipisequiped(2)) set @RESIST, @RESIST + 10 + getequiprefinerycnt(2);
	
	// * Chance to get minerals - (10 = 10%) - (20 = 5%) - (100 = 1%)
	set @HLuck,rand(1);
	mes "[^0000FF" + strcharinfo(0) + "^000000]";
	mes "Necesito encontrar el punto debil de este mineral lo mas pronto posible.";
	mes " ";
	mes ">> Equipo de Resistencia : ^FF0000" + @RESIST + "%^000000";
	next;
	
L_BrkRock:
	if (@HRock > 20)
		set @RockSt$,"^0EAE1B";
	else if (@HRock > 15)
		set @RockSt$,"^7CAE0E";
	else if (@HRock > 10)
		set @RockSt$,"^E3C521";
	else if (@HRock > 5)
		set @RockSt$,"^D86112";
	else
		set @RockSt$,"^D82412";
	
	if (@HPick > 20)
		set @PickSt$,"^0EAE1B";
	else if (@HPick > 15)
		set @PickSt$,"^7CAE0E";
	else if (@HPick > 10)
		set @PickSt$,"^E3C521";
	else if (@HPick > 5)
		set @PickSt$,"^D86112";
	else
		set @PickSt$,"^D82412";
	//dispbottom "PASO Inicial";
	for (set @x, 0; @x < 25; set @x, @x + 1) {
		//dispbottom "PASO 1 for";
		if (@HRock > @x)
			set @RockSt$, @RockSt$ + "¤";
		else
			set @RockSt$, @RockSt$ + "^E0E0E0¤";
		if (@HPick > @x)
			set @PickSt$, @PickSt$ + "¤";
		else
			set @PickSt$, @PickSt$ + "^E0E0E0¤";
	}
	//dispbottom "PASO 2";
	mes "[^0000FF" + strcharinfo(0) + "^000000]";
	if (@HWP)
		mes "Punto Débil: ^0000FF" + $@PickLocation$[@HWeak] + "^000000";
	else
		mes "Buscando punto debil...";
	//dispbottom "PASO 3";
	mes "> Rock: ";
	mes " [" + @RockSt$ + "^000000]";
	mes "> " + getitemname(7318) + ": ";
	mes " [" + @PickSt$ + "^000000]";
	next;
	//dispbottom "PASO 4";
	deletearray @KPos,127;
	deletearray @KNm$,127;
	
	/*setarray @KPos[0],50,50,50,50,50,50,50,50,50;
	set @i, 0;
	while(@i < 9) {
		//dispbottom "PASO 5 while";
		set @Wrd, rand(9);
		set @Repeated,0;
		if (@i > 0) {
			for (set @x, 0; @x < @i; set @x, @x + 1) {
				if (@Wrd == @KPos[@x]) set @Repeated,1;
			}
		}
		if (!@Repeated) {
			// Set the position in the array
			set @KPos[@i],@Wrd;
			set @i, @i + 1;
		}
	}*/
	
	// Random Menu Build
	set @i, 0;
	setarray @KPos[0],9,9,9,9,9,9,9,9,9;
	setarray @TPos[0],0,1,2,3,4,5,6,7,8;

	while( @i < 9 ) {
		set @Wrd, rand(9 - @i);

		set @KPos[@i], @TPos[@Wrd];
		deletearray @TPos[@Wrd],1;
		
		set @i, @i + 1;
	}
	
	//dispbottom "PASO 6";
	set @tmpMenu$,"";
	for (set @x, 0; @x < 9; set @x, @x + 1) {
		//dispbottom "PASO 7 for";
		set @tmpMenu$, @tmpMenu$ + $@PickLocation$[@KPos[@x]] + ":";
	}
	//dispbottom "PASO 8";
	set @tmpMenu$, @tmpMenu$ + "Exit";

	// Select the location to break the rock
	set @opc, select(@tmpMenu$) - 1;

	if (@opc == 9) goto L_Exit;

	// Weak side found!
	if (@KPos[@opc] == @HWeak) {
		soundeffect "chepet_attack.wav",0;
		set @HWP,1;
		set @HPower1, rand(3,4);
		set @HPower2, rand(2);
	} else {
		soundeffect "apocalips_h_move.wav",0;
		set @HPower1, rand(2);
		set @HPower2, rand(1,3);
	}
	//dispbottom "PASO 9";
	set @HRock, @HRock - @HPower1;
	set @HPick, @HPick - @HPower2;
	if (!rand(10)) {
		// Accident!!!
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		if (@RESIST < 100 || !rand(10)) {
			if (sex)
				soundeffect "die_male.wav",0;
			else
				soundeffect "die_merchant_female.wav",0;
			percentheal -rand(1,rand(100-@RESIST)),0;
			mes "^FF0000" + $@MNAccidents$[rand(getarraysize($@MNAccidents$))] + "^000000";
		} else {
			mes "^FF0000" + $@MNAccidents2$[rand(getarraysize($@MNAccidents2$))] + "^000000";
		}
		if (HP < 1) close;
		next;
	} else if (!rand(500)) {
		// Earth Quake!!!
		soundeffect "earth_quake.wav",0;
		percentheal -100,-100;
		announce "Tu mineral ha colapsado, estas muerto!",3;
		end;
	}
	//dispbottom "PASO 10";
	if (@HPick < 1) {
		// Old Pick is broken!
		specialeffect2 155;
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		mes "Mierda, he roto mi " + getitemname(7318) + "!";
		mes " ";
		mes "Me quedan >> ^FF0000" + countitem(7318) + "^000000 " + getitemname(7318) + " solamente.";
		close;
	}
	//dispbottom "PASO 11";
	// Give another chance while @HRock is > 0
	if (@HRock > 0) goto L_BrkRock;
	soundeffect "tming_success.wav",0;
	specialeffect 266;
	set MinaRota, MinaRota + 1;
	//dispbottom "PASO 12";
	mes "[^0000FF" + strcharinfo(0) + "^000000]";
	mes "Yeahh!!!, la pared de piedra es historia!";
	next;
	soundeffect "ice_titan_die.wav",0;
L_GetMinerals:
	//dispbottom "PASO 13 Minerals";
	if (@HPick > 20)
		set @PickSt$,"^0EAE1B";
	else if (@HPick > 15)
		set @PickSt$,"^7CAE0E";
	else if (@HPick > 10)
		set @PickSt$,"^E3C521";
	else if (@HPick > 5)
		set @PickSt$,"^D86112";
	else
		set @PickSt$,"^D82412";
	for (set @x, 0; @x < 25; set @x, @x + 1) {
		//dispbottom "PASO 14 Minerals for";
		if (@HPick > @x)
			set @PickSt$, @PickSt$ + "¤";
		else
			set @PickSt$, @PickSt$ + "^E0E0E0¤";
	}
	//dispbottom "PASO 15 Minerals";
	mes "[^0000FF" + strcharinfo(0) + "^000000]";
	mes "> " + getitemname(7318) + ": ";
	mes " [" + @PickSt$ + "^000000]";
	next;
	//dispbottom "PASO 16 Minerals";
	select("Cavar");
	// Clang!!!
	//dispbottom "PASO 17 Minerals";
	soundeffect "green_iguana_damage.wav",0;	
	set @Points, rand(@HLuck);
	//dispbottom "PASO 18 Minerals";
	if (!@Points) { // 10%
		set @IType, rand(1000);
		if (@IType == 1) { // 0.1%
			set @Mineral, $@BonusItems[rand(getarraysize($@BonusItems))];
			set @Message$, "OMG!!! encontre 1 " + getitemname(@Mineral) + "!";
		} else if (@IType < 500) { // 50%
			set @Mineral, $@SpecialMinerales[rand(getarraysize($@SpecialMinerales))];
			set @Message$, "Yeah!, esto es fantastico!";
		} else { // 94.9%
			set @Mineral, $@NormalMinerals[rand(getarraysize($@NormalMinerals))];
			set @Message$, "No hay nada fantastico aqui?";
		}
	} else { // 90%
		set @Mineral, $@CommonMinerals[rand(getarraysize($@CommonMinerals))];
		set @Message$, "Pfff...";
	}
	//dispbottom "PASO 19 Minerals";
	if (@Mineral > 500) {
		// Check weight
		if (checkweight(@Mineral,1) == 0) {
			mes "[^0000FF" + strcharinfo(0) + "^000000]";
			mes "Mierda!!!... necesito mas espacio para tomar esto...";
			mes "(Perdiste: ^FF00001 " + getitemname(@Mineral) + "^000000)";
			close;
		}
		getitem @Mineral,1;
		dispbottom "Mineral: Obtuviste 1 " + getitemname(@Mineral);
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		mes @Message$;
		next;
	} else {
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		mes ">> Grr!!... maldito mineral!!! <<";
		next;
	}
	//dispbottom "PASO 20 Minerals";
	// Calculate the Old Pick
	set @HPick, @HPick - rand(3);
	if (@HPick < 1) {
		// Old Pick is broken!
		specialeffect2 155;
		mes "[^0000FF" + strcharinfo(0) + "^000000]";
		mes "Mierda, He roto mi " + getitemname(7318) + ".";
		mes " ";
		mes ">> ^FF0000" + countitem(7318) + "^000000 " + getitemname(7318) + " menos";
		close;
	}
	//dispbottom "PASO 21 Minerals";
	goto L_GetMinerals;
L_Exit:
	mes "[^0000FF" + strcharinfo(0) + "^000000]";
	mes "Estoy cansado... fiuuufff...";
	close;
}

-	script	MiningTools	-1,{
	mes "[^0000FFMinero^000000]";
	mes "Hola " + strcharinfo(0) + ", deseas comprar item para la mineria?";
	next;
l_MainMenu:
	switch (select("Comprar Item","Acerca de la Mineria","Localizacion del Mineral","Adios!!")) {
		case 1:
			mes "[^0000FFMinero^000000]";
			mes "Gracias, tengo " + getitemname(7318) + " y algunas pociones para curar en el caso que tengas un accidente.";
			close2;
			callshop "MinerShop",1;
			end;
		case 2:
			mes "[^0000FFMinero^000000]";
			mes "El arte de la minería es la manera para obtener minerales.";
			next;
			mes "[^0000FFMinero^000000]";
			mes "Los minerales por lo general están ocultas bajo un muro de rocas que debes romper.";
			next;
			mes "[^0000FFMinero^000000]";
			mes "El muro de rocas es muy fuerte, pero un buen minero siempre busca un punto debil antes de romper sus herramientas.";
			next;
			mes "[^0000FFMinero^000000]";
			mes "Los minerales están llenos de secretos, algunos mineros dicen que se puede encontrar material valioso e incluso Tesoros.";
			next;
			mes "[^0000FFMinero^000000]";
			mes "Todo lo que te he dicho suena maravilloso, pero creeme, no siempre es asi...";
			next;
			mes "[^0000FFMinero^000000]";
			mes "El arte de la mineria es peligrosa, debes ir completamente equipado para protegerte.";
			next;
			mes "[^0000FFMinero^000000]";
			mes "He observado que los mineros prefieren ^FF0000" + getitemname(5009) + "^000000 y ^FF0000" + getitemname(5031) + "^000000";
			next;
			goto l_MainMenu;
		case 3:
			mes "[^0000FFMinero^000000]";
			mes "Por el momento solamente se han encontrado minerales en Anthell LVL 1 y en Payon Dungeon LVL 1.";
			next;
			goto l_MainMenu;
	}
	mes "[^0000FFMinero^000000]";
	mes "Un gusto verte " + strcharinfo(0) + ".";
	close;
}

-	script	Mineros	-1,{
      mes "[^0000FFBusca Tesoros^000000]";
      mes "En este sector hay tesoros,";
      mes "si quieres puedes buscar conmigo.";
      next;
      mes "[^0000FFBusca Tesoros^000000]";
      mes "Recuerda que debes tener el equipo adecuado para comenzar la";
      mes "busqueda, si no los tienes ve a hablar con el Gran Minero para que te ayude.";
      next;
      mes "[^0000FFBusca Tesoros^000000]";
      mes "Este se encuentra en la tienda de armas de Prontera, alli podrás encontrarlo.";
      next;
      mes "[^0000FFBusca Tesoros^000000]";
      mes "Suerte en tu busqueda....la necesitarás!!";
      close;

}

// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
// UBICACION DE MINERALES #001 - #999
// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
anthell01,92,271,5	duplicate(MinerSpot)	Mineral#001	1907
anthell01,93,271,5	duplicate(MinerSpot)	Mineral#002	1907
anthell01,94,271,5	duplicate(MinerSpot)	Mineral#003	1907
anthell01,95,271,5	duplicate(MinerSpot)	Mineral#004	1907
anthell01,96,271,5	duplicate(MinerSpot)	Mineral#005	1907
anthell01,97,271,5	duplicate(MinerSpot)	Mineral#006	1907

pay_dun00,147,173,5	duplicate(MinerSpot)	Mineral#007	1907
pay_dun00,148,173,5	duplicate(MinerSpot)	Mineral#008	1907
pay_dun00,149,173,5	duplicate(MinerSpot)	Mineral#009	1907
pay_dun00,150,173,5	duplicate(MinerSpot)	Mineral#010	1907
pay_dun00,151,173,5	duplicate(MinerSpot)	Mineral#011	1907
pay_dun00,152,173,5	duplicate(MinerSpot)	Mineral#012	1907
pay_dun00,153,173,5	duplicate(MinerSpot)	Mineral#013	1907
pay_dun00,154,173,5	duplicate(MinerSpot)	Mineral#014	1907
pay_dun00,155,173,5	duplicate(MinerSpot)	Mineral#015	1907
pay_dun00,156,173,5	duplicate(MinerSpot)	Mineral#016	1907

// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
// UBICACION DE LOS AYUDANTES
// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
anthell01,91,268,1	duplicate(Mineros)	Busca Tesoros#001	848
pay_dun00,148,170,1	duplicate(Mineros)	Busca Tesoros#002	848


// Add your own mines

// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
// Merchants
// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
//prt_in,160,139,5	duplicate(MiningTools)	Gran Minero#12	813

// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
// Shop
// o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o-o \\
-	shop	MinerShop	-1,7318:15000,501:-1,502:-1,503:-1,504:-1,5009:50000,5031:100000
