//===== rAthena Script =======================================
//    Extended Battleground
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
// Date:
//  10-10-2019
// By:
//  Easycore         (Zephyrus, original author)
//
// Contact:
//    - Facebook: https://www.facebook.com/EasycoreRo/
//    - rAthena:  https://rathena.org/board/profile/16425-easycore/
//    - Discord:  Easycore#3412
//= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

// ==============================================================================
// BattleGround System - Common NPCs
// ==============================================================================

// MapFlags
// *********************************************************************
bat_room	mapflag	nomemo
// bat_room	mapflag	nowarpto
bat_room	mapflag	nobranch
bat_room	mapflag	nopenalty
bat_room	mapflag	noteleport
bat_room	mapflag	nosave	SavePoint
//bat_room	mapflag	allow_bg_items // Funcion no agregada [Easycore]
// Mapflag Town is required to Join BG Queues
bat_room	mapflag	town
	
// Battleground Queue Handler
// *********************************************************************
-	script	BG_Queue_Join	-1,{
	end;
	

OnInit:
	// Configuration
	// *****************************************************
	// Battleground Rotation - Main NPC name
	setarray .BG_Arenas$[0],		"Flavius_CTF",		"Flavius_TD",			"Conquest",		"Rush";
	// Battleground Arena Name - for announcements
	setarray .BG_Names$[0],		"Capture the Flag",	"Team DeathMatch",		"Conquest",		"Rush";
	// Minimum Players to start the BG
	setarray .BG_Min[0],	              3,               3,                3,            3;
	// Maximum Players per Team
	setarray .BG_Max[0],	             50,              50,               50,           50;          
	
	// BG Message Colors
	setarray .BG_Color$[0],	                "0xA0522D",    "0x808000",      "0xFFA500",       "0xDDA0DD",		"0x696969",		"0x4169E1",		"0x4169E1",		"0x3399FF",		"0x9ACD32";
	setarray .BG_Color2[0],	                0xA0522D,    0x808000,      0xFFA500,       0xDDA0DD,		0x696969,		0x4169E1,		0x4169E1,		0x3399FF,		0x9ACD32;
	// Team Building Mode : 0 = Lineal | 1 = Random | 2 = Class Priority | 3 = Faction Mode | 4 = Team Color (by script set Bat_Team,N;)
	set .TeamMode, 1;
	// Happy Hour - 1: Enabled | 0: Disabled
	set .HappyHour, 1; 
	// Happy Hour - Reward rates %
	set .HappyHourRates, 100 + 20; // +20%
	// Main Code
	// *****************************************************
	set .BG_Count, getarraysize(.BG_Arenas$);
	set .BG_Queue, bg_queue_create("Battleground Arena","BG_Queue_Join::OnJoinEvent",80);
	set .VS_Queue, bg_queue_create("Free For All Arena","BG_Queue_Join::OnVersusJoin",80);
	set .HWE, 0;
	// Move to Next Arena
	if ($BG_Index >= .BG_Count) set $BG_Index,0; // Restart Rotation
	set .TotalArenas, getarraysize(.BG_Arenas$);
	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	set .Ready, 1;
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method
	/* Commands */
	// Admin/GM Commands
	bindatcmd "bgstart","BG_Queue_Join::OnBGStartCommand",99,99;
	bindatcmd "bgstop","BG_Queue_Join::OnBGStopCommand",99,99;
	bindatcmd "bgmode","BG_Queue_Join::OnBGModeCommand",99,99;		// [Easycore]
	bindatcmd "bgsize","BG_Queue_Join::OnBGSizeCommand",99,99;		// [Easycore]
	bindatcmd "bghappyhour","BG_Queue_Join::OnBGHHCommand",99,99;	// [Easycore]
	// Player Commands
	bindatcmd "joinbg","BG_Queue_Join::OnDoJoin",0,99;
	bindatcmd "leavebg","BG_Queue_Join::OnDoLeave",0,99;
	end;

OnDoJoin: // @joinbg
	if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	} else if (agitcheck() || agitcheck2()) {
		dispbottom "[Battleground is currently disabled for this moment due to War of Emperium.]";
		end;
	} else if (checkquest(8600,PLAYTIME) == 2) {
		erasequest 8600;
	} else if (checkquest(8600,PLAYTIME) == 0) {
		dispbottom "[You are a Deserter. You can't participate until the indicator goes off]";
		end;
	}

	bg_queue_join .BG_Queue;
	end;

OnDoLeave: // @leavebg
	bg_queue_leave .BG_Queue;
	end;
// Easycore
// @bgmode <1-10>
OnBGModeCommand:
	.@x = atoi(.@atcmd_parameters$[0]);
	if (.@x <= .TotalArenas+1 && .@x > 0) {
		if (.@x == 1)
			set $BG_Index, rand(.TotalArenas)-1;
		else
			set $BG_Index, .@x-3;
		message strcharinfo(0),"["+.BG_Names$[$BG_Index+1]+"] has been set.";
		donpcevent "BG_Queue_Join::OnRotate";
		end;
	} else {
		message strcharinfo(0), "Usage: @bgmode <1-"+(.TotalArenas+1)+">";
		dispbottom "#1 - Random",0xFFFFFF;
		for (.@i = 0; .@i<.TotalArenas; .@i++)
			dispbottom "#"+(.@i+2)+" - "+.BG_Names$[.@i],.BG_Color2[.@i];			
		end;
	}
		
// @bgsize <1-10> <min> <max>
OnBGSizeCommand:
	.@x = atoi(.@atcmd_parameters$[0]);
	.@x2 = atoi(.@atcmd_parameters$[1]);
	.@x3 = atoi(.@atcmd_parameters$[2]);
	if (.@x2 == 0 || .@x3 == 0) {
		message strcharinfo(0), "Usage: @bgsize <0-"+.TotalArenas+"> <min players> <max players>";
		dispbottom "#0 - All Arenas",0xFFFFFF;
		for (.@i = 0; .@i<.TotalArenas; .@i++)
			dispbottom "#"+(.@i+1)+" - "+.BG_Names$[.@i],.BG_Color2[.@i];			
		end;
	} else if (.@x2 > .@x3) {
		message strcharinfo(0), "@bgsize failed: min players is higher than max players. ";
		end;
	}	
	if (.@x <= .TotalArenas && .@x > 0) {
		message strcharinfo(0),"["+.BG_Names$[.@x-1]+"] min players is <"+.@x2+"> and max players is <"+.@x3+">.";
		setarray .BG_Min[.@x-1],.@x2;
		setarray .BG_Max[.@x-1],.@x3;
	} else {
		message strcharinfo(0),"[All Arenas] min players is <"+.@x2+"> and max players is <"+.@x3+">.";
		setarray .BG_Min[0],.@x2,.@x2,.@x2,.@x2,.@x2,.@x2,.@x2,.@x2,.@x2;
		setarray .BG_Max[0],.@x3,.@x3,.@x3,.@x3,.@x3,.@x3,.@x3,.@x3,.@x3;
	}
	end;
// @bghappyhour
OnBGHHCommand:
	if (.HappyHour == 0) {
		message strcharinfo(0),"Happy Hour is currently disabled.";
		end;
	}
	if (getbattleflag("bg_eAmod_mode") == 0) {
		message strcharinfo(0),"Battlegrounds are currently disabled.";
		end;
	}
	if (!.HWE)
		donpcevent "BG_Queue_Join::OnDoHappyHour";
	else
		donpcevent "BG_Queue_Join::OnEndHappyHour";
	end;
OnBGStartCommand:
	if (getbattleflag("bg_eAmod_mode") == 1) {
		message strcharinfo(0),"Battlegrounds is already enabled.";
		end;
	}
	setbattleflag "bg_eAmod_mode", 1;
	message strcharinfo(0),"Battlegrounds have been enabled.";
	donpcevent "BG_Queue_Join::OnRotate";
	callsub OnEndArena;
OnBGStopCommand:
	setbattleflag "bg_eAmod_mode", 0;
	set .HWE, 0;
	setbattleflag "bg_reward_rates",100; // Normal Rates
	message strcharinfo(0),"Battlegrounds have been disabled.";
	.@msg$ = "[Battlegrounds] Arenas and Queuing have been disabled by a Game Master.";
	announce .@msg$, 0, 0x00FFFF, 0x190, 12, 100, 100;
	if ($@BG_Status != 0)
		donpcevent .BG_Arena$+"::OnMatchEnd";
	callsub OnEndArena;
OnEndArena:
	set $@BG_Status, 0;
	if ($@BG_Team1 == 0 && $@BG_Team2 == 0 && $@BG_Team3 == 0)
		end;
	// Warps Teams
	bg_warp $@BG_Team1,"bat_room",155,150;
	bg_warp $@BG_Team2,"bat_room",155,150;
	bg_warp $@BG_Team3,"bat_room",155,150;
	// Destroy Teams
	bg_destroy $@BG_Team1; set $@BG_Team1, 0;
	bg_destroy $@BG_Team2; set $@BG_Team2, 0;
	bg_destroy $@BG_Team3; set $@BG_Team3, 0;
OnRotate:
// OnTimer100000: // Rotation if BG don't start... Comment if you don't want to use this method. 120 = 2 Minutes
	// Check if BG is enabled/disabled [Easycore]
	if (getbattleflag("bg_eAmod_mode") == 0) end;

	// Move to Next Arena
	if (set($BG_Index,$BG_Index + 1) >= .BG_Count)
		set $BG_Index,0; // Restart Rotation

	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

OnJoinEvent:
	if ($@BG_Status == 2)
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch($@BG_Status) {
		case 0: // Idle - Ready to Start
			if (.BG_Arena$ == "Tierra_TI")
				set .@Req, .BG_Min[$BG_Index] * 3;
			else
				set .@Req, .BG_Min[$BG_Index] * 2;

			if (bg_queue_checkstart(.BG_Queue,.TeamMode,((.BG_Arena$ == "Tierra_TI") ? 3 : 2),.BG_Min[$BG_Index])) {
				donpcevent .BG_Arena$ + "::OnBuildTeams";

				// Fill Teams with Queue Members			
				if (.BG_Arenas$[$BG_Index] == "Tierra_TI")
					bg_queue2teams .BG_Queue,.BG_Min[$BG_Index],.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2,$@BG_Team3;
				else
					bg_queue2teams .BG_Queue,.BG_Min[$BG_Index],.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2;

				stopnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

				set $@BG_Status,1;
				set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- Begining";
				bgannounce .@msg$,.BG_Color$[$BG_Index];

				donpcevent .BG_Arena$ + "::OnReady";
			} else {
				set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- " + (.@Req - .@Queue_Count) + " more players to start.";
				bgannounce .@msg$,.BG_Color$[$BG_Index];
			}
			break;
		case 1: // Running - Others can join
			if (.@Queue_Count > 0) {
				if (.BG_Arena$ == "Tierra_TI") {
					bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2,$@BG_Team3;
					set .@BG_Count1, bg_get_data($@BG_Team1,0);
					set .@BG_Count2, bg_get_data($@BG_Team2,0);
					set .@BG_Count3, bg_get_data($@BG_Team3,0);
					set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + ", G: " + .@BG_Count3 + "/" + .BG_Max[$BG_Index] + " (Playing)";
				} else {
					bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2;
					set .@BG_Count1, bg_get_data($@BG_Team1,0);
					set .@BG_Count2, bg_get_data($@BG_Team2,0);
					set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + " (Playing)";
				}

				bgannounce .@msg$,.BG_Color$[$BG_Index];
			}
			break;
	}
	end;

OnVersusJoin:
	if ($@VS_Status != 0)
		end;

	set .@Queue_Count, bg_queue_data(.VS_Queue,0);
	if (.@Queue_Count >= 10) {
		donpcevent "FFA_Arena::OnBuildTeams";
		bg_queue2teams .VS_Queue,1,1,0,$@VS_Team[0],$@VS_Team[1],$@VS_Team[2],$@VS_Team[3],$@VS_Team[4],$@VS_Team[5],$@VS_Team[6],$@VS_Team[7],$@VS_Team[8],$@VS_Team[9];
		set $@VS_Status,1;
		announce "Battleground -- Free For All -- Begining",0,0x483D8B;
		donpcevent "FFA_Arena::OnReady";
	}
	else
		announce "Battleground -- Free For All -- " + (10 - .@Queue_Count) + " more players to start",1,0x483D8B;
	end;

OnFri0600: // Friday, 6 a.m.
	//rankreset 1; // Ranking Reset
	end;

// BattleGround Happy Hour
OnDoHappyHour:
OnClock1400:
OnClock1600:
	if (getbattleflag("bg_eAmod_mode") == 0 || .HappyHour == 0) end;
	set .HWE, 1;
	announce "-- Battleground Happy Hour has begun --",0,0x00FF00;
	setbattleflag "bg_reward_rates",.HappyHourRates; // +20% Reward Rates
	end;
OnEndHappyHour:
OnClock2200:
OnClock0000:
	if (getbattleflag("bg_eAmod_mode") == 0 || .HappyHour == 0) end;
	set .HWE, 0;
	announce "-- Battleground Happy Hour is over --",0,0x00BFFF;
	setbattleflag "bg_reward_rates",100; // Normal Rates
	end;
}

// Registration NPC
// *********************************************************************
-	script	BG_Register	FAKE_NPC,{

	if (getvariableofnpc(.Ready,"BG_Queue_Join") == 0)
		donpcevent "BG_Queue_Join::OnInit";

	mes "[^FFA500Battle Recruiter^000000]";
	mes "This is the Battleground Information and Registration service.";
	mes "What do you want to do?";
	next;
	switch(select("^FFA500Battleground Arenas^000000:^0000FFFree For All Arena^000000:Warp to Battle Room")) {
	case 1:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "Battlegrounds Arena are different kinds of games where Teams fight for victory.";
		mes "What do you want to do?";
		next;
		switch(select("Register:Party Register:Leave Queue:Battleground Help")) {
		case 1:
			if (BaseLevel < 80)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Min required level to join a battleground is 80.";
				close;
			}
			if (checkquest(8600,PLAYTIME) == 2)
				erasequest 8600;
			if (checkquest(8600,PLAYTIME) == 1)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are a Deserter. You can't participate until the indicator goes off";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_join getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 2:
			if (getcharid(1) == 0)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are not on a Party.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_partyjoin getcharid(1),getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 3:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "If you are registered, you will be removed.";
			mes "Are you sure?";
			next;
			if (select("Yes, leave queue:No, I will stay") == 2)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Ok, nothing to change.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Close this window to continue...";
			close2;
			bg_queue_leave getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 4:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "Tell me... what battleground you don't understand?";
			next;
			switch(select("Capture the Flag:Team DeathMatch:Stone Control:Eye of Storm:Bossnia:Domination:Triple Inferno:Conquest:Rush"))
			{
			case 1:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The objective of the Flavius Battle CTF is to score 3 points before your enemy, by capture their Flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "To capture a Flag you need to take the enemy flag, and bring it to your base flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If your base flag is captured too, you need to kill the flag carrier and take the flag back to your base.";
				break;
			case 2:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Kill all the enemy players to let their Team without points.";
				mes "Protect our army.";
				break;
			case 3:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Take the Stones in the middle of the battlefield and put in on your base, in the Stone Points.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each Stone will give points to your team. First team reach 99 points wins the game.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Protect your stones from to be captured by the enemy.";
				break;
			case 4:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are two bases, North and South which teams can capture by holding position on the Base more than the other team.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each base will give you points each 5 seconds of Domination. If your Team control both bases the amount of points increases.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "To get aditional points, in the middle there is a Flag spawn, capture it and put it on any of your team Bases.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The first Team reach 99 points wins the match.";
				break;
			case 5:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Attack the enemy base and destroy each MVP Guardian. To do damage to the guardian your team must capture the Balance Flag in the middle base.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each team have 5 guardian to be protected or killed.";
				break;
			case 6:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are three bases, North, Center and South which teams can capture by holding position on the Base more than the other team.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Each base will give you points each 5 seconds of Domination.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "The first Team reach 99 points wins the match.";
				break;
			case 7:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "There are 3 teams in the battlefield, your team and other 2 enemies.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Kill the enemy players, collect the skulls and bring then to the Sacrifice Totem to win points.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You can collect your own team skulls and bring then to your Sacrifice Totem to avoid other teams to score.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you get killed all your skulls will be drop to the floor, including your own skull. First Team to get 80 points wins the battle.";
				break;
			case 8:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you are Attacking, destroy the enemy defenses and it's Flag.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you are Defending, protect your castle defenses and the Flag.";
				break;
			case 9:
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Fight to capture the Castle and organize your team to defend it.";
				next;
				mes "[^FFA500Battle Recruiter^000000]";
				mes "If you fail on the first capture, kill the defender and take it for your team.";
				break;
			}
			break;
		}
		close;
	case 2:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "Free For All Arena is a Duel between 10 players with No Teams.";
		mes "The target is to kill 25 players.";
		mes "What do you want to do?";
		next;
		switch(select("Register:Leave Queue")) {
		case 1:
			if (BaseLevel < 80)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Min required level to join a battleground is 80.";
				close;
			}
			if (checkquest(8600,PLAYTIME) == 2)
				erasequest 8600;
			if (checkquest(8600,PLAYTIME) == 1)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "You are a Deserter. You can't participate until the indicator goes off";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Let's proceed with the registration...";
			mes "You must wait on any City until BG starts.";
			mes "Close this window to continue...";
			close2;
			bg_queue_join getvariableofnpc(.VS_Queue,"BG_Queue_Join");
			end;
		case 2:
			mes "[^FFA500Battle Recruiter^000000]";
			mes "If you are registered, you will be removed.";
			mes "Are you sure?";
			next;
			if (select("Yes, leave queue:No, I will stay") == 2)
			{
				mes "[^FFA500Battle Recruiter^000000]";
				mes "Ok, nothing to change.";
				close;
			}

			mes "[^FFA500Battle Recruiter^000000]";
			mes "Close this window to continue...";
			close2;
			bg_queue_leave getvariableofnpc(.VS_Queue,"BG_Queue_Join");
			end;
		}
		break;
	case 3:
		mes "[^FFA500Battle Recruiter^000000]";
		mes "May the War God bless you.";
		close2;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// bat_room,151,159,3	duplicate(BG_Register)	Battle Recruiter#bat	4_F_JOB_KNIGHT
// prontera,123,83,3	duplicate(BG_Register)	Battle Recruiter#prt	4_F_JOB_KNIGHT

// General Guillaume
// *********************************************************************
bat_room,160,159,3	script	General Guillaume	4_M_KY_KIYOM,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Aventurer"+(Sex?"o":"a")+" de sangre caliente, necesitamos tu habilidad para ganar esta batalla.";
	next;
	cutin "bat_kiyom1",2;
	mes "[General Guillaume]";
	mes "Nuestro gran rey, Marcel Marollo VII, está muy enfermo últimamente. Su Majestad ha declarado que nos eligió a mí o al Príncipe Croix como el próximo rey entre sus 9 hijos.";
	next;
	mes "[General Guillaume]";
	mes "¡Dos reyes no pueden compartir una nación!";
	mes "Solo el que salga victorioso de la batalla señalada por Su Majestad será entronizado.";
	next;
	mes "[General Guillaume]";
	mes "Sin embargo, esto no es solo una batalla entre nosotros. Esta batalla determinará el futuro de este país. Prometo por mi honor demostrar que soy yo quien puede proteger a este Maroll de las amenazas externas.";
	next;
	switch(select("^5533ff [>] ^000000 Unirse al equipo azul","^5533ff [>] ^000000 Unirse al equipo verde","^5533ff [>] ^000000 Finalizar conversación")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bienvenido a mi ejército, camarada.";
			mes "Tus ojos me dicen que eres un soldado en el que puedo confiar.";
			set Bat_Team,1;
			next;
			mes "[General Guillaume]";
			mes "Ahora, sube las escaleras y solicita la batalla con tus camaradas. ¡Seguro que te recibirán de todo corazón!";
			break;
		case 2:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Así que tomarás esta pelea como tuya...";
			mes "Entonces eres mi enemig"+(Sex?"o":"a")+" .";
			set Bat_Team,0;
			next;
			mes "[General Guillaume]";
			mes "Ve ahora, nos encontraremos de nuevo en el campo de batalla.";
			break;
		case 3:
			mes "[General Guillaume]";
			mes "¡Seré yo quien capture la bandera!";
			break;
	}

	close2;
	cutin "",255;
	end;
}

// General Croix
// *********************************************************************
bat_room,160,140,3	script	Prince Croix	4_M_CRU_CRUA,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Sabio aventurer"+(Sex?"o":"a")+", ¿por qué no nos prestas tu poder para la victoria?";
	next;
	cutin "bat_crua2",2;
	mes "[Prince Croix]";
	mes "No deseo derramar sangre, pero no tengo más remedio que luchar por la posibilidad de la paz y por el bien de mi pueblo.";
	next;
	mes "[Prince Croix]";
	mes "El general Guillaume puede tener una ventaja en esta batalla, ya que es el gran general de Maroll, pero eso no significa que ganará automáticamente. Quiero ganar esta batalla para poder otorgar un mejor futuro a mi pueblo.";
	next;
	
	switch(select("^5533ff [>] ^000000 Unirse al equipo rojo","^5533ff [>] ^000000 Unirse al equipo verde","^5533ff [>] ^000000 Finalizar conversación")) {
		case 1:
			mes "[Prince Croix]";
			mes "Muchas gracias. Siento que puedo ganar con la ayuda de aventureros como tú. ¡Ahora, baja las escaleras y únete a tus camaradas para mejorar sus habilidades para luchar contra el enemigo!";
			set Bat_Team,2;
			break;
		case 2:
			mes "[Prince Croix]";
			mes "Así que tomarás esta pelea como tuya...";
			mes "Entonces eres mi enemig"+(Sex?"o":"a")+" .";
			set Bat_Team,0;
			next;
			mes "[Prince Croix]";
			mes "Ve ahora, nos encontraremos de nuevo en el campo de batalla.";
			break;
		case 3:
			mes "[Prince Croix]";
			mes "¡Por Maroll!";
			break;
	}	
	
	close2;
	cutin "",255;
	end;
}

// Guard Dummy
// *********************************************************************

bat_room,161,141,3	script	Prince Croix's Aid::bat_aid	4_M_CRU_HEAD,{ end; }
bat_room,161,139,3	duplicate(bat_aid)	Prince Croix's Aid::bat_aid2	4_M_CRU_HEAD
bat_room,161,160,3	duplicate(bat_aid)	General Guillaume's Aid::bat_aid3	4_M_KY_HEAD
bat_room,161,158,3	duplicate(bat_aid)	General Guillaume's Aid::bat_aid4	4_M_KY_HEAD

// Flags
// *********************************************************************

-	script	Base Flag#bg	FAKE_NPC,{ end; }

// BattleGround Warper - Exit
// *********************************************************************

bat_room,148,150,4	script	Teletransportador#bat	10056,{
	cutin "verus_ian01",2;
	mes "[Teletransportador]";
	mes "¿Deseas abandonar el campo de batalla? Usa mi servicio para volver a la ciudad.";
	next;
	if (select("^5533ff [>] ^000000 Abandonar:^5533ff [>] ^000000 Quedarse") == 2) {
		mes "[Teletransportador]";
		mes "Estaré aquí para cuando necesites de mi servicio.";
		cutin "",255;
		close;
	}
	set .@spoint$, getsavepoint(0);
	set .@x, getsavepoint(1);
	set .@y, getsavepoint(2);
	mes "[Teletransportador]";
	mes "Serás enviado de vuelta a " + .@spoint$ + ".";
	close2;
	warp .@spoint$, .@x, .@y;
	cutin "",255;
	end;
}

// Kafra
// *********************************************************************
/* bat_room,148,147,4	script	Kafra Staff::kaf_bat	4_F_KAFRA9,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
} */

// == Badges Exchange =======================================
bat_room,1,1,3	itemshop	BraveryExchanger	-1,7828,13036:100,13411:100,1425:100,1632:100,1634:100,1543:100,1924:100,1978:100,1574:100,1824:100,1183:100,1380:100,13305:100,1279:100,1739:100,13108:100,13172:100,2538:50,2539:50,2540:50,2435:50,2436:50,2437:50,2376:80,2377:80,2378:80,2379:80,2380:80,2381:80,2382:80,2720:500,2721:500,2722:500,2723:500,2724:500,2725:500,2733:500
bat_room,1,1,3	itemshop	ValorExchanger	-1,7829,13037:100,13410:100,1633:100,1635:100,1542:100,1923:100,1977:100,1575:100,1823:100,1184:100,1482:100,1379:100,13306:100,1280:100,1738:100,13171:100,13173:100,13174:100,2538:50,2539:50,2540:50,2435:50,2436:50,2437:50,2376:80,2377:80,2378:80,2379:80,2380:80,2381:80,2382:80,2720:500,2721:500,2722:500,2723:500,2724:500,2725:500,2733:500
bat_room,1,1,3	itemshop	HeroismExchanger	-1,7773,1187:2000,1281:2000,1282:2000,1310:2000,1382:2000,1426:2000,1486:2000,1546:2000,1576:2000,1577:2000,1640:2000,1641:2000,1743:2000,1826:2000,1827:2000,1927:2000,1981:2000,2002:2000,13042:2000,13110:2000,13176:2000,13177:2000,13178:2000,13179:2000,13307:2000,13416:2000,13417:2000,13418:2000,2394:840,2395:170,2396:25,2444:580,2445:70,2446:15,2549:630,2772:1200,2773:280,2774:40

bat_room,160,150,3	script	Erundek	10123,{
	cutin "mark_wt_1",2;
	mes "[Erundek]";
	mes "Bienvenid"+(Sex?"o":"a")+", poderos"+(Sex?"o":"a")+" guerrer"+(Sex?"o":"a")+".";
	mes "¿Qué puedo hacer por ti hoy ?";
	next;
	switch( select("^5533ff [>] ^000000 Intercambiar Bravery Badges","^5533ff [>] ^000000 Intercambiar Valor Badges","^5533ff [>] ^000000 Intercambiar Heroism Badges","^5533ff [>] ^000000 Hmm, nada, supongo.") )
	{
		case 4:
			mes "[Erundek]";
			mes "Como desées.";
			mes "Te veo luego.";
			cutin "",255;
			close;
		case 1: // Bravery Badges
			mes "[Erundek]";
			mes "Entonces quieres intercambiar ^0000FFBravery Badges^000000 de la Batalla del Valle de Tierra.";
			mes "Cierra esta ventana para abrir la tienda.";
			cutin "",255;
			close2;
			callshop "BraveryExchanger",0;
			end;
		case 2: // Valor Badges
			mes "[Erundek]";
			mes "Entonces quieres intercambiar ^FF0000Valor Badges^000000 de la Batalla de Flavio.";
			mes "Cierra esta ventana para abrir la tienda.";
			cutin "",255;
			close2;
			callshop "ValorExchanger",0;
			end;
		case 3: // Heroism Badges
			mes "[Erundek]";
			mes "Entonces quieres intercambiar ^FFA500Heroism Badges^000000 de la Batalla de Kriger Von Midgard.";
			mes "Cierra esta ventana para abrir la tienda.";
			cutin "",255;
			close2;
			callshop "HeroismExchanger",0;
			end;
	}
}

bat_room,160,148,3	script	Telma	10122,{
	cutin "ep16cook_king_1",2;
	mes "[Telma]";
	mes "Bienvenid"+(Sex?"o":"a")+", poderos"+(Sex?"o":"a")+" guerrer"+(Sex?"o":"a")+".";
	mes "¿Necesitas suministros para tus batallas?";
	mes "Puedo intercambiar suministros por tus medallas...";
	next;
	
	switch(select("^5533ff [>] ^000000 150 Condensed White Potion:^5533ff [>] ^000000 90 Blue Potion:^5533ff [>] ^000000 3 Poison Bottle:^5533ff [>] ^000000 30 Fire Bottle:^5533ff [>] ^000000 30 Acid Bottle:^5533ff [>] ^000000 30 Plant Bottle:^5533ff [>] ^000000 30 Marine Sphere Bottle:^5533ff [>] ^000000 15 Glistening Coat:^5533ff [>] ^000000 50 Yellow Gemstone:^5533ff [>] ^000000 50 Red Gemstone:^5533ff [>] ^000000 100 Blue Gemstone:^5533ff [>] ^000000 5 Speed Potion:^5533ff [>] ^000000 20 Cobweb:^5533ff [>] ^000000 25 Light Concentration Potion:^5533ff [>] ^000000 25 Light Awakening Potion:^5533ff [>] ^000000 25 Light Bersek Potion")) {
		case 1:		callsub S_BuyConsumableBG,547,150;	// Condensed White Potion
		case 2:		callsub S_BuyConsumableBG,505,90;	// Blue Potion
		case 3:		callsub S_BuyConsumableBG,678,3;	// Poison Bottle
		case 4:		callsub S_BuyConsumableBG,7135,30;	// Fire Bottle
		case 5:		callsub S_BuyConsumableBG,7136,30;	// Acid Bottle
		case 6:		callsub S_BuyConsumableBG,7137,30;	// Plant Bottle
		case 7:		callsub S_BuyConsumableBG,7138,30;	// Marine Sphere Bottle
		case 8:		callsub S_BuyConsumableBG,7139,15;	// Glistening Coat
		case 9:		callsub S_BuyConsumableBG,715,50;	// Yellow Gemstone
		case 10:	callsub S_BuyConsumableBG,716,50;	// Red Gemstone
		case 11:	callsub S_BuyConsumableBG,717,100;	// Blue Gemstone
		case 12:	callsub S_BuyConsumableBG,12016,5;	// Speed Potion
		case 13:	callsub S_BuyConsumableBG,1025,20;	// Cobweb
		case 14:	callsub S_BuyConsumableBG,14509,25;	// Light Concentration Potion
		case 15:	callsub S_BuyConsumableBG,14510,25;	// Light Awakening Potion
		case 16:	callsub S_BuyConsumableBG,14511,25;	// Light Bersek Potion
	}
	cutin "",255;
	end;

S_BuyConsumableBG:
	set .@item_id, getarg(0);
	set .@amt, getarg(1);
	
	mes "[Telma]";
	mes "¿Lo quieres para?...";
	next;
	if (select("^5533ff [>] ^000000 Battleground:^5533ff [>] ^000000 War of Emperium") == 1) .@bg_item = 1;
	mes "[Telma]";
	mes "How many sets of ^0000FF"+ .@amt +" "+ getitemname(.@item_id) +"s^000000 do you want?";
	mes "1 = "+ .@amt   +" suministros = 1 medalla";
	mes "2 = "+ .@amt*2 +" suministros = 2 medallas";
	mes "3 = "+ .@amt*3 +" suministros = 3 medallas";
	mes "etc..";
	next;
	input .@sets;
	mes "[Telma]";
	if (.@sets < 1) {
		mes "Ingrese un numero positivo y mayor que cero.";
		cutin "",255;
		close;
	}
	mes "Entonces quieres ^0000FF"+ (.@amt*.@sets) +" "+ getitemname(.@item_id) +"s^000000...";
	mes "Recuerde que estos solo se pueden usar en "+((.@item_id)?"BattleGround":"WoE Maps");
	mes "Te costará "+ .@sets +" medalla(s)...";
	next;
	switch(select("Darle "+ .@sets +" Bravery Badge(s):Darle "+ .@sets +" Valor Badge(s):Darle "+ (3*.@sets) +" Heroism Badges")) {
		case 1: set .@badge,7828; set .@cost,1; break;
		case 2: set .@badge,7829; set .@cost,1; break;
		case 3: set .@badge,7773; set .@cost,3; break;
	}
	.@bgchar = getBattleFlag("bg_reserved_char_id");
	.@woechar = getBattleFlag("woe_reserved_char_id");
	mes "[Telma]";
	if (countitem(.@badge) < (.@cost*.@sets)) {
		mes "No tienes suficiente ^FF0000"+getitemname(.@badge)+"s^000000 para comprar "+(.@amt*.@sets)+" "+getitemname(.@item_id)+"s.";
	} else if (!checkweight(.@item_id, .@amt*.@sets)) {
		mes "Tienes sobrepeso o tienes demasiados artículos en su inventario.";
	} else {
		delitem .@badge, .@cost*.@sets;
		if (.@bg_item) // Battleground's Consumables
			getitem2 .@item_id,(.@amt*.@sets),1,0,0, 254, 0, .@bgchar & 0xFFFF, .@bgchar >> 0x10;
		else
			getitem2 .@item_id,(.@amt*.@sets),1,0,0, 254, 0, .@woechar & 0xFFFF, .@woechar >> 0x10;
		mes "Aquí están tus: ^0000FF"+(.@amt*.@sets)+" "+getitemname(.@item_id)+"s^000000!";
		mes "Recuerda que solo se pueden utilizar en "+((.@item_id)?"BattleGround":"WoE Maps");
	}
	cutin "",255;
	close;
}

-	script	stop_die	-1,{
OnInit:
	bindatcmd "die",strnpcinfo(0) +"::OnPCDie";
	end;
OnPCDie:
	if( getmapflag( strcharinfo(3),mf_battleground ) && getgmlevel() < 10 )
		message strcharinfo(0),"No puedes usar @die en los mapas de Battleground.";
	else
		atcommand "@die";
	end;
}