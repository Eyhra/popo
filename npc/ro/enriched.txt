//===== rAthena Script =======================================
//= Advanced Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Changelog: ===========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//= 1.7 Removed re-roll behavior. [Secret]
//= 1.8 Traducción al Español. [NRCRO]
//============================================================

seal_in,26,33,6	script	Völundr#cash	681,{
	disable_items;
	mes "[Völundr]";
	mes "Yo soy tu Armero";
	mes "Puedo refinar todo tipo de armas,";
	mes "armadura y equipo, así que dime";
	mes "que quieres refinar.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Völundr]";
		mes "No creo que pueda refinar ningún artículo que tengas...";
		close;
	}
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Völundr]";
		mes "Ve a buscar otro Herrero.No puedes refinar este objeto.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Völundr]";
		mes "Hmm... alguien ya perfeccionó esto. No creo que pueda trabajar más en ello.";
		close;
	}

	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	.@price = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);

	// Make sure you have the necessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	callsub S_RefineValidate,getequipweaponlv(.@part),.@material,.@price,.@part,.@refineitemid,.@refinerycnt;

	mes "[Völundr]";
	mes "Clang! Clang! Clang!";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[Völundr]";
		mes "¡Ahí tienes! Esta listo.";
		mes "Ha pasado un tiempo desde que hice un refinamiento de "+((getequipweaponlv(.@part))?"arma":"armadura")+". ¡Debes estar feliz porque se ha vuelto más fuerte!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[Völundr]";
	mes "Uuuuuuuuuummmmmph!!!";
	next;
	mes "[Völundr]";
	mes "...";
	mes ".....";
	mes ".......Huhuhuhuhu~";
	mes "........Fue tu elección y mi habilidad, no te arrepientas..";
	close;

S_RefineValidate:
	.@weapon_lvl = getarg(0);
	.@item_req = getarg(1);
	.@price = getarg(2);
	.@part = getarg(3);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(.@weapon_lvl){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}

	mes "[Völundr]";
	if (.@weapon_lvl)
		mes "Quieres refinar un nivel "+ .@weapon_lvl +" weapon?";
	mes "Para refinar eso, necesitarás tener un ^ff9999"+ getitemname(.@item_req) +"^000000 y "+ .@price +" zeny.";
	mes "¿Te gustaria continuar?";
	next;
	if(select("Si:No") == 1) {
		if (getequippercentrefinery(.@part) < 100) {
			if (.@weapon_lvl) {
				mes "[Völundr]";
				mes "Wow!!";
				mes "Esta arma probablemente";
				mes "Parece que ha sido refinada ...";
				mes "muchas veces...";
				mes "Puede romperse si";
				mes "lo refinas de nuevo.";
				next;
				mes "Y si se rompe,";
				mes "¡Ya no puedes usarlo!";
				mes "Todas las cartas que contiene y las propiedades. ^ff0000se perderá^000000!";
				mes "^ff0000Además, ¡el equipo se romperá!^000000";
				mes "¿Estás segur"+(Sex?"o":"a")+" de que todavía quieres continuar?";
				next;
			if(select("Si:No") == 2) {
					mes "[Völundr]";
					mes "Bueno.";
					mes "Porque si el arma se rompe por un refinamiento irrazonable, entonces también me pongo de mal humor.";
					close;
				}
			} else {
				mes "[Völundr]";
				mes "Giggle. Giggle. Oh, tienes agallas, atreviéndose a refinar esto.";
				mes "Sabes que es bastante arriesgado, ¿no?";
				next;
				mes "Si tu equipo defensivo está roto, nunca podrás volver a usarlo.";
				mes "Incluso sus cartas y sus modificaciones ^ff0000desapareceran por completo^000000.";
				//mes "Everything will disappear. As in... GONE!";
				mes "¿De verdad deseas continuar?";
				next;
				if(select("Si:No") == 2) {
					mes "[Völundr]";
					mes "Qué absurdo. Haces perder mi precioso tiempo.";
					mes "Piérdete, mocos"+(Sex?"o":"a")+".";
					close;
				}
			}
		}
		if (countitem(.@item_req) > 0 && Zeny > .@price) {
			delitem .@item_req,1;
			set Zeny, Zeny - .@price;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, getarg(4)) ||
				callfunc("F_IsEquipRefineHack", .@part, getarg(5)) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
				mes "[Holink]";
				emotion ET_FRET;
				mes "Espera un segundo...";
				mes "¿Crees que soy estúpido?";
				mes "¡Cambiaste el objeto mientras yo no miraba! ¡Sal de aquí!";
				close;
			}

			return;
		}
		mes "[Völundr]";
		mes "¿Estos son todos los que tienes?";
		mes "Lo siento mucho, pero no puedo hacer nada sin todos los materiales. Además, Merezco algunos pagos por mi trabajo, ¿no?";
		close;
	}
	mes "[Völundr]";
	mes "No puedo evitarlo incluso si no estás content"+(Sex?"o":"a")+" por eso ...";
	close;
}