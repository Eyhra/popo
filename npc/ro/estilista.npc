//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//= 1.2 Translated to spanish [DanielArt]
//= 1.3 Updated script to C++. [DanielArt]
//============================================================

seal,69,101,4	script	Estilista::STYLE	10182,{
    cutin "ep16_tes01",2;
	mes "[^FF7070Estilista^000000]";
	mes "Oh, ¡hola!","Dime, ¿quieres quedar más guap"+(Sex?"o":"a")+" con mi ayuda?";
	setarray .@Styles[1],
		getbattleflag("max_cloth_color"),
		getbattleflag("max_hair_style"),
		getbattleflag("max_hair_color");
	setarray .@Look[1],
		LOOK_CLOTHES_COLOR,
		LOOK_HAIR,
		LOOK_HAIR_COLOR;
	next;
	.@s = select("^5533ff [>] ^000000Color de ropa:^5533ff [>] ^000000Estilo de peinado:^5533ff [>] ^000000Color de peinado:^5533ff [>] ^000000Estilo al azar:^777777Salir^000000");
	if(.@s == 5) {
		mes "[^FF7070Estilista^000000]";
		mes "Bueno, tu sabrás lo que te conviene, vuelve cuando quieras.";
		cutin "",255;
		close;
	}
	.@Revert = getlook(.@Look[.@s]);
	.@Style = 1;
	if(.@s == 4) {
		for(.@i = 1; .@i < getarraysize(.@Look); .@i++)
			setarray .@rev_rand[.@i],getlook(.@Look[.@i]);
		while(1) {
			for(.@i = 1; .@i < getarraysize(.@Look); .@i++) {
				.@r = rand(1,.@Styles[.@i]);
				if(.@Look[.@i] == LOOK_HAIR && .@r == 56)
					continue;
				setlook .@Look[.@i], .@r;
			}
			switch(select("~ Probar otro:~ Regresar al normal:~ Conservar este")) {
				case 1:
					break;
				case 2:
					for(.@i = 1; .@i < getarraysize(.@Look); .@i++)
						setlook .@Look[.@i], .@rev_rand[.@i];
					mes "[^FF7070Estilista^000000]";
					mes "Vuelve cuando quieras.";
					cutin "",255;
					close;
				case 3:
					mes "[^FF7070Estilista^000000]";
					mes "Perfecto estilo, vuelve cuando quieras.";
					cutin "",255;
					close;
			}
		}
	}
	while(1) {
		setlook .@Look[.@s], .@Style;
		message strcharinfo(0),"Este es el estilo #"+.@Style+".";
		.@menu$ = " ^777777~^000000 Siguiente (^0055FF"+((.@Style!=.@Styles[.@s])?.@Style+1:1)+"^000000): ^777777~^000000 Anterior (^0055FF"+((.@Style!=1)?.@Style-1:.@Styles[.@s])+"^000000): ^777777~^000000 Ir a...: ^777777~^000000 Revertir al original (^0055FF"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
		case 1:
			.@Style = ((.@Style != .@Styles[.@s]) ? .@Style+1 : 1);
			if(.@Style == 56 && .@Look[.@s] == LOOK_HAIR)
				.@Style++;
			break;
		case 2:
			.@Style = ((.@Style != 1) ? .@Style-1 : .@Styles[.@s]);
			if(.@Style == 56 && .@Look[.@s] == LOOK_HAIR)
				.@Style--;
			break;
		case 3:
			message strcharinfo(0),"Elige un estilo del 1 al "+.@Styles[.@s]+".";
			input .@Style,0,.@Styles[.@s];
			if (!.@Style || .@Style == 56 && .@Look[.@s] == LOOK_HAIR)
				.@Style = rand(1,.@Styles[.@s]);
			break;
		case 4:
			.@Style = .@Revert;
			setlook .@Look[.@s], .@Revert;
			break;
		}
	}
	cutin "",255;
	end;
}

gld2_prt.gat,84,114,4	duplicate(STYLE)	Estilista#144-1	10182
prontera.gat,169,180,4	duplicate(STYLE)	Estilista#145-1	10182
