 // ________                   .__         .__      _____             __
// \______ \  _____     ____  |__|  ____  |  |    /  _  \  _______ _/  |_
//  |    |  \ \__  \   /    \ |  |_/ __ \ |  |   /  /_\  \ \_  __ \\   __\
//  |    `   \ / __ \_|   |  \|  |\  ___/ |  |__/    |    \ |  | \/ |  |
// /_______  /(____  /|___|  /|__| \___  >|____/\____|__  / |__|    |__|
//         \/      \/      \/          \/               \/
//                             www.danielart.net
//==========================================================================
// ---                    Hunting                      ---
//========================================================
// ---                  by DanielArt                   ---
//========================================================

// Tienda de premios
// Editar después del ':0,', con el formato ID:Precio en puntos
-	pointshop	HUNT_SHOP	-1,Mission_Points:0,9036:500,9046:500,9049:500,9033:500,9053:500,9030:500,9034:500,9028:500,9032:500,9041:500,9043:500,9050:500,9048:500,9038:500,9054:500,9035:500,9044:500,9031:500,9051:500,9037:500,9045:500,9047:500,10025:50,10032:50,10027:50,10028:50,10037:50,10036:50:10034:100,10026:50,10030:50,10029:50,10033:50,10031:50,10024:50,10022:50,10023:50,10038:50,6115:1,6114:1,6096:1,6095:1,6097:1,6107:1,6109:1,6105:1,6106:1,6099:1,6112:1,6110:1,6113:1,6104:1,6100:1,6108:1,6098:1,6111:1,554:1,7824:1,7823:1,7822:1,7821:1,7766:1,6115:1,19291:500,31614:500,31686:500,31437:500,31512:500,31456:500,31186:500,31452:500,31299:500

britoniah,103,25,4	script	Cacería::hunting_m	10194,20,20,{
	cutin "ep16_seiren01",2;
	mes "[^cc6600Cacería^000000]";
	mes "Hola ^ff8800"+strcharinfo(0)+"^000000.";
	mes "¡¿A qué es un gran día para salir de cacería?!";
	mes "¿En qué te puedo ayudar?";
	next;
	.@m$ = ((Mission_Tick > 0) ? "^ff0000Info. Misión / Entregar / Cambiar^000000:" : "^ff7070Iniciar misión de cacería^000000:") + "Mejor Cazador:^5533ffTienda de cacería^000000:^777777Salir^000000";
	switch(select(.@m$)) {
		case 1:
			if( Mission_Tick <= 0 || Mission_Tick < gettimetick(2) ) {
				if( Mission_Tick < gettimetick(2) ) {
					mes "[^cc6600Cacería^000000]";
					if( Mission_Tick == 0 ) {
						mes "Aquí te asignaré ^0000FFMisiones de Cacería^000000 cada día.";
						mes "Las misiones constan de eliminar criaturas que te asignaré.";
						next;
						mes "[^cc6600Cacería^000000]";
						mes "Cada vez que las completes te asignaré más difíciles.";
						next;
						mes "[^cc6600Cacería^000000]";
						mes "Pero no te preocupes, puedes realizar el trabajo en grupos de amigos, y cualquiera del equipo puede ser quien elimine y a ti se te cuenta.";
						next;
						mes "[^cc6600Cacería^000000]";
						mes "Tendrás exactamente 24 horas para terminar una misión, si la terminas antes del tiempo debes venir a mi para pagarte el trabajo.";
					} else if( Mission_Ended ) {
						mes "Tu nueva misión está lista, ya revisé los documentos.";
						mes "¿Estás tu listo para recibirla?";
					} else {
						mes "Según puedo ver fallaste tu última misión, no llegaste antes de las 24 horas o no eliminaste el total de criaturas solicitadas.";
						next;
						mes "[^cc6600Cacería^000000]";
						mes "En fin, una lástima. Supongo que vienes por tu misión de hoy.";
						mes "¿Estás list"+(Sex?"o":"a")+" para llevarla a cabo?";
					}
					next;
					if( select("Si, ¿cuál es mi trabajo del día?:No gracias, por hoy no.") == 2 ) {
						mes "[^cc6600Cacería^000000]";
						mes "Suerte entonces, vuelve cuando cuentes con tiempo y recibirás buena paga.";
						close3;
					}
					mes "[^cc6600Cacería^000000]";
					mes "Un momento, dejar revisar mis documentos para ver qué te puedo asignar...";
					next;
S_RestartMission:
					Mission_Tick = gettimetick(2) + 86400;
					.@Mr = ((Mission_Rank % 45) * 2) + 1;
					.@Mb = (Mission_Rank / 45) * 5;
					.@Mr += .@Mb;
					if( .@Mr > 140 )
						.@Mr = 140;
					Mission_Ended = 0;
					// Cantidad de monstruos
					.@CantidadMobs = rand(80,99);
					// Incremento de monstruos en base al nivel base
					.@CantidadMobs += BaseLevel / 18;
					// Rango de Nivel de monstruos
					.@MobRank = 14;
					.@Mid = getmobrandid(.@Mr, .@MobRank);
					Mission_ID1 = .@Mid;
					Mission_Count1 = .@CantidadMobs;
					callfunc "MissionInfo";
					next;
					mes "[^cc6600Cacería^000000]";
					mes "Suerte en tu misión, recuerda que tienes 24 horas para completarla.";
					mes "Presentate conmigo cuando termines.";
					close3;
				}
			} else {
				mes "[^cc6600Cacería^000000]";
				mes "Me alegra verte nuevamente por aquí, veamos como vas con el trabajo que te encargué...";
				next;
				switch(select("^ff0000Info. Misión / Entregar^000000:Cambiar mi misión:^777777Salir^000000")) {
					case 1:
						if( Mission_Count1 < 1 ) {
							mes "[^cc6600Cacería^000000]";
							if( Mission_Ended == 0 ) {
								mes "¡¡Excelente trabajo!! Terminaste tu misión del día sin problemas y antes del tiempo límite.";
								mes "Dejame pagarte por tus servicios.";
								next;
								.@Mid = Mission_ID1;
								.@Mlv += strmobinfo(3, .@Mid);
								.@ExpBase += (strmobinfo(6, .@Mid) * 20);
								.@ExpJob += (strmobinfo(7, .@Mid) * 20);
								.@ZenyPremio = .@Mlv * 150;

								// Puntos a entregar por dificultad
								if( .@Mlv < 125 )
									.@PuntosCaza = 7;
								else if( .@Mlv < 250 )
									.@PuntosCaza = 14;
								else
									.@PuntosCaza = 25;

								// Entrega de Premios
								getexp .@ExpBase, .@ExpJob;
								Zeny += .@ZenyPremio;
								Mission_Points += .@PuntosCaza;

								if( Mission_Rank < 449 )
									Mission_Rank++;
								Mission_Completed++;
								if( Mission_Completed > $TMission_Score ) {
									$TMission_Score = Mission_Completed;
									$TMission_CharID = getcharid(0);
									$TMission_Name$ = strcharinfo(0);
									$TMission_Job$ = jobname(Class);
								}
								Mission_ID1 = 0;
								Mission_Count1 = 0;
								Mission_Ended = 1;
							}
							mes "Tu misión diaria ya esta terminada.";
							.@Mtick = Mission_Tick - gettimetick(2);
							if( .@Mtick > 3600 )
								mes "Te asignaré una nueva misión en ^0000FF" + (.@Mtick / 3600) + " horas^000000.";
							else if( .@Mtick > 60 )
								mes "Te asignaré una nueva misión en ^0000FF" + (.@Mtick / 60) + " minutos^000000.";
							else if( .@Mtick > 0 )
								mes "Te asignaré una nueva misión en ^0000FF" + (.@Mtick) + " segundos^000000.";
							else
								mes "Dejame ordenar mis documentos, vuelve a hablarme para asignarte tu nueva misión.";
							close3;
						}
						callfunc "MissionInfo";
						close3;
					case 2:
						mes "[^cc6600Cacería^000000]";
						mes "Claro que podemos cambiar tu misión.";
						next;
						if(select("No, continuaré igual:Reiniciar mi misión") == 1) {
							mes "[^cc6600Cacería^000000]";
							mes "Perfecto, eso se espera de un verdadero cazador.";
							close3;
						}
						/*if(Zeny < 5000) {
							mes "[^cc6600Cacería^000000]";
							mes "No dispones de la suficiente cantidad de Zeny.";
							close3;
						}*/
						mes "[^cc6600Cacería^000000]";
						mes "De acuerdo, veamos entonces.";
						//Zeny -= 5000;
						Mission_ID1 = 0;
						Mission_Count1 = 0;
						Mission_Tick = 0;
						next;
						goto S_RestartMission;
				}
			}
			close3;
		case 2:
			mes "[^cc6600Cacería^000000]";
			if($TMission_Score) {
				mes "El mejor cazador actual es:";
				mes " ^ff0000-^000000 Nombre:^5533ff "+$TMission_Name$+"^000000.";
				mes " ^ff0000-^000000 Clase:^5533ff "+$TMission_Job$+"^000000.";
				mes " ^ff0000-^000000 Misiones:^5533ff "+$TMission_Score+"^000000.";
			} else
				mes "Aún no hay un mejor cazador, vuelve luego.";
			close3;
		case 3:
			mes "[^cc6600Cacería^000000]";
			mes "Cuando completes una misión recibirás ^5533ffpuntos de cacería^000000 que podrás usar en la tienda que te mostraré.";
			close2;
			cutin "",255;
			callshop "HUNT_SHOP",1;
			end;
		case 4:
			mes "[^cc6600Cacería^000000]";
			mes "Vuelve cuando quieras emprender una nueva misión de cacería.";
			close3;
	}
	end;
OnPCLoadMapEvent:
	if(strcharinfo(3) == "prontera" && !Mission_Tick)
		showevent QTYPE_QUEST,1;
	end;
OnInit:
	bindatcmd "hunting","hunting_m::OnCommand",0,99;
	end;
OnTouch:
	if( Mission_Tick )
		end;
	if( Mission_Tick <= 0 || Mission_Tick < gettimetick(2) || Mission_Ended )
		showevent QTYPE_QUEST;
	else if( Mission_Count1 < 1)
		showevent QTYPE_QUEST2;
	end;
OnCommand:
	if(Mission_Tick == 0) {
		dispbottom "@hunting - No tienes misiones de cacería.";
		end;
	}
	dispbottom "@hunting - Tu cacería actual es:";
	.@Mid = Mission_ID1;
	dispbottom " > "+Mission_Count1+ " x " + strmobinfo(2,.@Mid) + " ~ (ID : " + .@Mid + ")";
	end;
}

function	script	MissionInfo	{
	.@ExpJob = .@Mlv = .@ExpBase = 0;
	mes "[^cc6600Contrato - Misión^000000]";
	.@Mid = Mission_ID1;
	mes "^0000FF" + Mission_Count1 + "^000000 x ^cc6600" + strmobinfo(2,.@Mid) + "^000000 (ID : " + .@Mid + ")";
	.@Mlv += strmobinfo(3, .@Mid);
	.@ExpBase += (strmobinfo(6, .@Mid) * 20);
	.@ExpJob += (strmobinfo(7, .@Mid) * 20);
	.@ZenyPremio = .@Mlv * 150;
	next;
	mes "[^cc6600Detalles - Misión^000000]";
	mes "Nivel : ^0000FF" + .@Mlv + "^000000";
	if( .@Mlv < 125 ) {
		mes "Dificultad : ^008000Bajo^000000";
		.@PuntosCaza = 7;
	} else if( .@Mlv < 250 ) {
		mes "Dificultad : ^000080Media^000000";
		.@PuntosCaza = 14;
	} else {
		mes "Dificultad : ^FF0000Alta000000";
		.@PuntosCaza = 25;
	}
	.@Mtick = Mission_Tick - gettimetick(2);
	if( .@Mtick <= 0 )
		mes "Tiempo Restante : ^FF0000Expirado^000000";
	else if( .@Mtick > 3600 )
		mes "Tiempo Restante : ^0000FF" + (.@Mtick / 3600) + " hora(s)^000000.";
	else if( .@Mtick > 60 )
		mes "Tiempo Restante : ^0000FF" + (.@Mtick / 60) + " minuto(s)^000000.";
	else
		mes "Tiempo Restante : ^0000FF" + (.@Mtick) + " segundo(s)^000000.";
	next;
	mes "[^cc6600Premio - Misión^000000]";
	mes "Base Exp : ^0000FF" + callfunc("F_InsertComma",.@ExpBase) + "^777777 exp^000000.";
	mes "Job Exp : ^0000FF" + callfunc("F_InsertComma",.@ExpJob) + "^777777 exp^000000.";
	mes "Zeny : ^0000FF" + callfunc("F_InsertComma",.@ZenyPremio) + "^777777 z^000000.";
	mes "Puntos : ^0000FF" + callfunc("F_InsertComma",.@PuntosCaza) + "^777777 puntos^000000.";
	return 0;
}
