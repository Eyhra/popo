//= ==== rAthena Script ============================================= =\\
//= Restocking                                                         \\
//= ==== By: ======================================================== =\\
//= Vykimo and Aglao                                                   \\
//= ==== Current Version: =========================================== =\\
//= Version: 11.1                                                      \\
//= ==== Compatible With: =========================================== =\\
//= rAthena SVN                  				       \\
//= ==== Additional Comments: ======================================= =\\
//= A restocking modification, changing property items with            \\
//  WOE/BG Tags for consumibles and ammunitions.                       \\
//  WARNING: Don't distribute or resell this script/modification.      \\
//= ================================================================= =\\

-	script	restocking_atcmd	-1,{

function	TrouverIndice	{
	for(.@i=0;.@i<getarraysize(getarg(0));.@i++)
		if(getelementofarray(getarg(0),.@i) == getarg(1)) break;
		
	if(.@i == getarraysize(getarg(0)) && getelementofarray(getarg(0),.@i) != getarg(1)) 
		return -1;
	else return .@i;
}

OnInit:
	// Requisito para no vips
	// -1 para deshabilitar
	.CoinID = -1;
	// ===================
	bindatcmd "restock",strnpcinfo(3)+"::OnAtcommand";
	end;
	
OnAtcommand:
	if(.@atcmd_numparameters == 1 && strtolower(.@atcmd_parameters$[0]) == "off") {
		deletearray RestockList[0],getarraysize(RestockList);
		dispbottom "@restock : Lista borrada.";
		end;
	}
	if(.@atcmd_numparameters == 0) {
		dispbottom "Uso de comando: @restock <itemid> <itemid> ...";
	} else {
		if(.CoinID > 0 && !vip_status(1)) {
			mes "[@Restock]";
			mes "You are not a VIP-player, you have to pay 1x "+getitemname(.CoinID)+" to use this command.";
			mes "^0000FFCommand Usage : @restock <itemid> <itemid> ...^000000";
			next;
			if(select("Pay 1x "+getitemname(.CoinID)+":Cancel") == 2) close;
			if(countitem(.CoinID)<1) {
				mes "[@Restock]";
				mes "You don't have enough "+getitemname(.CoinID)+".";
				close;
			}
			close2;
		}
		.@pay = 0;
		for(.@i=0;.@i<.@atcmd_numparameters;.@i++) {
			.@id = atoi(.@atcmd_parameters$[.@i]);
			if(getitemname(.@id) != "null") {
				if(getiteminfo(.@id,2) == 0 || getiteminfo(.@id,2) == 2 || getiteminfo(.@id,2) == 3 || getiteminfo(.@id,2) == 10 || getiteminfo(.@id,2) == 11) {	
					if((.@array_id = TrouverIndice(RestockList,.@id)) < 0) {
						setarray RestockList[getarraysize(RestockList)],.@id;
						dispbottom "@restock : Item '"+getitemname(.@id)+"' (id:"+.@id+") ¡Agregado exitosamente!";
						.@pay = 1;
					} else {
						dispbottom "@restock : Item '"+getitemname(.@id)+"' (id:"+.@id+") ya en la lista.";
					}
				}
				else dispbottom "@restock : Solo puedes agregar item consumibles y ammunitions. (id "+.@id+" non available)";
			} else dispbottom "@restock : este item no existe. (id "+.@id+")";
		}
		dispbottom "@restock - Lista actual:";
		for(.@i=0;.@i<getarraysize(RestockList);.@i++)
			dispbottom "- "+getitemname(RestockList[.@i])+" ("+storagecountitem(RestockList[.@i])+" en storage)";
		if(.@i == 0)
			dispbottom "Vacío.";
		
		if(.CoinID > 0 && !vip_status(1) && .@pay == 1) {
			dispbottom ".: "+ getitemname(.CoinID) +" consumed :.";
			delitem .CoinID,1;
		}
	}
	end;
OnItemUsed:
	set .@useditem,itemusedid;
	set .@useditem2,itemusedcard2;
	set .@useditem3,itemusedcard3;
	set .@useditemamount,itemusedquantity;
	//announce "Usando item con ID "+.@useditem+", cantidad "+.@useditemamount+" y con flag "+.@useditem2+" junto con "+.@useditem3,0;

	if((.@useditem > 0 || .@useditem2 > 0) && (.@array_id = TrouverIndice(RestockList,.@useditem)) >= 0) {
		if(storagecountitem(.@useditem) > 0 && .@useditem2 > 0 && .@useditem3 > 0) {
			storagedelitem2 .@useditem,.@useditemamount,1,0,0,254,0,.@useditem2,.@useditem3;
			//announce "Usando consume con restock con flag",0;
			getitem2 .@useditem,.@useditemamount,1,0,0,254,0,.@useditem2,.@useditem3;
		}
		else if(storagecountitem(.@useditem) > 0 && .@useditem2 == 0 && .@useditem3 == 0) {
			storagedelitem .@useditem,.@useditemamount;
			//announce "Usando consume con restock",0;
			getitem .@useditem,.@useditemamount;
		} else {
			//announce "Deletearray item restock",0;
			deletearray RestockList[.@array_id],1;
		}
	}
	end;
}
